10 GOSUB 1000
20 GOSUB 2000
30 GOSUB 3000
35 GOSUB 4000
40 GOSUB 5000
50 IF C$<>"" THEN GOSUB 4000
60 IF C$<>"" THEN GOSUB 6000
70 IF C$<>"" THEN GOSUB 7000
80 IF C$<>"" THEN GOSUB 8000
90 GOTO 40
100 PRINT CHR$(240+1)
110 PRINT CHR$(224+6)
120 PRINT AT(0,0), CHR$(222)
130 END
1000 REM SUBROUTINE TITLE
1010 PRINT CHR$(240+2)
1020 PRINT CHR$(224+12)
1030 PRINT CHR$(222)
1040 PRINT AT(0,0), "HANGMAN"
1050 PRINT "YOUR GOAL IS TO GUESS A WORD."
1060 PRINT "ENTER A LETTER BY HITTING THE KEY."
1070 PRINT "A LETTER WILL BE SHOWN IF YOU ARE RIGHT."
1080 PRINT "IF YOU ARE WRONG TOO OFTEN YOU WILL LOSE."
1090 PRINT "PRESS META TO QUIT THE GAME AT ANY TIME."
1100 PRINT "PRESS CODY TO CONTINUE..."
1110 I = 0
1120 K=PEEK(18)
1130 IF K=30 THEN GOTO 1160
1140 I = I + 1
1150 GOTO 1120
1160 PRINT CHR$(222)
1170 RETURN
2000 REM SUBROUTINE RANDOM WORD
2010 W$ = "SECRET"
2020 IF MOD(I,26)=0 THEN W$ = "APPLE"
2030 IF MOD(I,26)=1 THEN W$ = "BOOK"
2040 IF MOD(I,26)=2 THEN W$ = "CODY"
2050 IF MOD(I,26)=3 THEN W$ = "DOG"
2060 IF MOD(I,26)=4 THEN W$ = "ELEPHANT"
2070 IF MOD(I,26)=5 THEN W$ = "FRIEND"
2080 IF MOD(I,26)=6 THEN W$ = "GAME"
2090 IF MOD(I,26)=7 THEN W$ = "HOUSE"
2100 IF MOD(I,26)=8 THEN W$ = "ISLAND"
2110 IF MOD(I,26)=9 THEN W$ = "JUICE"
2120 IF MOD(I,26)=10 THEN W$ = "KEY"
2130 IF MOD(I,26)=11 THEN W$ = "LOVE"
2140 IF MOD(I,26)=12 THEN W$ = "MUSIC"
2150 IF MOD(I,26)=13 THEN W$ = "NIGHT"
2160 IF MOD(I,26)=14 THEN W$ = "ORANGE"
2170 IF MOD(I,26)=15 THEN W$ = "PEOPLE"
2180 IF MOD(I,26)=16 THEN W$ = "QUEEN"
2190 IF MOD(I,26)=17 THEN W$ = "RIVER"
2200 IF MOD(I,26)=18 THEN W$ = "SUN"
2210 IF MOD(I,26)=19 THEN W$ = "TREE"
2220 IF MOD(I,26)=20 THEN W$ = "UMBRELLA"
2230 IF MOD(I,26)=21 THEN W$ = "VILLAGE"
2240 IF MOD(I,26)=22 THEN W$ = "WATER"
2250 IF MOD(I,26)=23 THEN W$ = "XYLOPHONE"
2260 IF MOD(I,26)=24 THEN W$ = "YELLOW"
2270 IF MOD(I,26)=25 THEN W$ = "ZOO"
2280 I$ = ""
2290 C$ = ""
2300 RETURN
3000 REM SUBROUTINE INIT V
3010 REM V(I)=0 letter not visible
3020 REM V(I)=1 letter visible 
3040 FOR I=0 TO LEN(W$)
3050 V(I) = 0
3060 NEXT
3070 RETURN
4000 REM SUBROUTINE UNVEIL CHAR AND PRINT WORD 
4010 PRINT AT(0,0), "WORD LEN:", LEN(W$)
4030 FOR I=0 TO LEN(W$)
4040 X$ = SUB$(W$, I, 1)
4050 IF X$=C$ THEN V(I)=1
4070 IF V(I) = 1 THEN PRINT AT(I,1), SUB$(W$, I, 1)
4080 IF V(I) = 0 THEN PRINT AT(I,1), CHR$(134)
4090 NEXT
4110 PRINT "YOU TRIED:", I$
4120 PRINT "MISTAKES:", LEN(I$)
4130 RETURN
5000 REM SUBROUTINE EVAL INPUT
5010 C$ = ""
5020 K=PEEK(16)
5030 IF K=15 THEN C$="O"
5040 IF K=23 THEN C$="U"
5050 IF K=27 THEN C$="T"
5060 IF K=29 THEN C$="E"
5070 IF K=30 THEN C$="Q"
5080 K=PEEK(17)
5090 IF K=15 THEN C$="L"
5100 IF K=23 THEN C$="J"
5110 IF K=27 THEN C$="G"
5120 IF K=29 THEN C$="D"
5130 IF K=30 THEN C$="A"
5140 K=PEEK(18)
5150 IF K=15 THEN GOTO 100
5160 IF K=27 THEN C$="V"
5170 IF K=29 THEN C$="X"
5180 IF K=23 THEN C$="N"
5190 K=PEEK(19)
5200 IF K=23 THEN C$="M"
5210 IF K=27 THEN C$="B"
5220 IF K=29 THEN C$="C"
5230 IF K=30 THEN C$="Z"
5240 K=PEEK(20)
5250 IF K=23 THEN C$="K"
5260 IF K=27 THEN C$="H"
5270 IF K=29 THEN C$="F"
5280 IF K=30 THEN C$="S"
5290 K=PEEK(21)
5300 IF K=15 THEN C$="P"
5310 IF K=23 THEN C$="I"
5320 IF K=27 THEN C$="Y"
5330 IF K=29 THEN C$="R"
5340 IF K=30 THEN C$="W"
5450 RETURN
6000 REM SUBROUTINE UPDATE INPUT
6010 N=0
6020 FOR I=0 TO LEN(I$)
6030 X$ = SUB$(I$, I, 1)
6040 IF C$=X$ THEN N=1
6050 NEXT
6060 FOR I=0 TO LEN(W$)
6070 X$ = SUB$(W$, I, 1)
6080 IF C$=X$ THEN N=1
6090 NEXT
6100 J$ = I$
6110 IF N=0 THEN J$ = I$ + C$
6120 I$ = J$
6900 RETURN
7000 REM SUBROUTINE WIN CHECK
7010 B=1
7020 FOR I=0 TO LEN(W$)
7030 IF V(I) = 0 THEN B=0
7040 NEXT
7050 IF B=0 THEN RETURN
7060 IF B=1 THEN PRINT "YOU WON! PRESS ARROW TO QUIT."
7070 K=PEEK(19)
7080 IF K=15 THEN GOTO 100
7090 GOTO 7070
8000 REM SUBROUTINE DRAW HANGMAN
8010 PRINT AT(20,23), CHR$(134), CHR$(180), CHR$(134), CHR$(134), CHR$(134)
8020 PRINT AT(25,23), CHR$(134), CHR$(134), CHR$(134), CHR$(134), CHR$(134)
8025 IF LEN(I$)=0 THEN RETURN
8030 PRINT AT(21,22), CHR$(156)
8040 PRINT AT(21,21), CHR$(156)
8050 PRINT AT(21,20), CHR$(156)
8060 PRINT AT(21,19), CHR$(156)
8070 PRINT AT(21,18), CHR$(156)
8080 PRINT AT(21,17), CHR$(156)
8090 PRINT AT(21,16), CHR$(156)
8100 PRINT AT(21,15), CHR$(156)
8110 PRINT AT(21,14), CHR$(156)
8120 PRINT AT(21,13), CHR$(156)
8130 PRINT AT(21,12), CHR$(156)
8140 PRINT AT(21,11), CHR$(156)
8145 IF LEN(I$)=1 THEN RETURN
8150 PRINT AT(21,10), CHR$(158), CHR$(134), CHR$(134), CHR$(134), CHR$(134)
8160 PRINT AT(26,10), CHR$(158), CHR$(134), CHR$(134), CHR$(134)
8170 PRINT AT(26,11), CHR$(156)
8180 PRINT AT(26,12), CHR$(156)
8190 PRINT AT(25,13), CHR$(152), CHR$(134), CHR$(140)
8195 IF LEN(I$)=2 THEN RETURN
8200 PRINT AT(25,14), CHR$(151)
8210 PRINT AT(27,14), CHR$(170)
8220 PRINT AT(25,15), CHR$(151)
8230 PRINT AT(27,15), CHR$(170)
8235 IF LEN(I$)=3 THEN RETURN
8240 PRINT AT(25,16), CHR$(141), CHR$(134), CHR$(142) 
8250 PRINT AT(25,17), CHR$(145), CHR$(156), CHR$(144)
8260 PRINT AT(26,18), CHR$(156)
8265 IF LEN(I$)=4 THEN RETURN
8270 PRINT AT(25,19), CHR$(145)
8280 PRINT AT(27,19), CHR$(144)
8290 PRINT AT(25,14), CHR$(151), CHR$(120), CHR$(120)
8300 PRINT AT(0,5), "YOU LOSE! PRESS ARROW TO QUIT."
8310 K=PEEK(19)
8320 IF K=15 THEN GOTO 100
8330 GOTO 8310
8900 RETURN

